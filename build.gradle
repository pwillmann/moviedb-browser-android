import com.android.build.gradle.AppPlugin
import com.android.build.gradle.LibraryPlugin

buildscript {
    ext.compileSdk = 28
    ext.minSdk = 21
    ext.targetSdk = 28
    ext.buildToolsVersion = '28.0.2'
    ext.kotlinVersion = '1.3.11'
    ext.supportLibVersion = '1.0.0-beta01'
    ext.androidKtxVersion = '1.0.1'
    ext.lifecycleVersion = '2.0.0-beta01'
    ext.epoxyVersion = '2.17.0'
    ext.mvRxVersion = '0.6.0'
    ext.rxJavaVersion = '2.2.1'
    ext.rxAndroidVersion = '2.1.0'
    ext.moshiVersion = '1.8.0'
    ext.koinVersion = '1.0.2'
    ext.retrofitVersion = '2.4.0'
    ext.navVersion = '1.0.0-alpha09'
    ext.constraintLayoutVersion = '2.0.0-alpha3'
    ext.glideVersion = '4.8.0'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

subprojects {
    project.pluginManager.apply("com.diffplug.gradle.spotless")

    spotless {
        java {
            // This is required otherwise the code in android modules isn't picked up by spotless.
            target("**/*.java")
            trimTrailingWhitespace()
            removeUnusedImports()
            googleJavaFormat()
        }

        kotlin {
            target("**/*.kt")
            ktlint("0.29.0").userData(["indent_size": "4", "android": "true", "max_line_length": "200"])
        }

        kotlinGradle {
            target("**/*.gradle.kts")
            ktlint("0.29.0").userData(["indent_size": "4", "android": "true", "max_line_length": "200"])
        }

        format("misc") {
            target("**/.gitignore", "**/*.gradle", "**/*.md", "**/*.sh", "**/*.yml")
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}

