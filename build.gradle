buildscript {
    ext.compileSdk = 28
    ext.minSdk = 21
    ext.targetSdk = 28
    ext.buildToolsVersion = '28.0.2'
    ext.kotlinVersion = '1.2.70'
    ext.supportLibVersion = '28.0.0-rc02'
    ext.androidKtxVersion = '1.0.0-rc02'
    ext.lifecycleVersion = '1.1.1'
    ext.epoxyVersion = '2.17.0'
    ext.mvRxVersion = '0.5.0'
    ext.rxJavaVersion = '2.2.1'
    ext.rxAndroidVersion = '2.1.0'
    ext.moshiVersion = '1.6.0'
    ext.koinVersion = '0.9.3'
    ext.retrofitVersion = '2.4.0'
    ext.navVersion = '1.0.0-alpha05'
    ext.constraintLayoutVersion = '2.0.0-alpha2'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha10'
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.14.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

subprojects {
    project.pluginManager.apply("com.diffplug.gradle.spotless")

    spotless {
        java {
            // This is required otherwise the code in android modules isn't picked up by spotless.
            target("**/*.java")
            trimTrailingWhitespace()
            removeUnusedImports()
            googleJavaFormat()
        }

        kotlin {
            target("**/*.kt")
            ktlint("0.24.0").userData(["indent_size": "4", "android": "true", "max_line_length": "200"])
        }

        kotlinGradle {
            target("**/*.gradle.kts")
            ktlint("0.24.0").userData(["indent_size": "4", "android": "true", "max_line_length": "200"])
        }

        format("misc") {
            target("**/.gitignore", "**/*.gradle", "**/*.md", "**/*.sh", "**/*.yml")
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}
